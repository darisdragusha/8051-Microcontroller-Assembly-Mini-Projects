;P1.0 AND P1.1 SELECT THE ROW TO BE TRANSMITTED. TRANSMIT IT THROUGH TX. DISPLAY THE CHAR BEING TRANSMITTED AND THE NUMBERS OF CHARS LEFT TO TRANSMIT

	ORG	0000H
	LJMP	INIT

INIT:
	MOV	SCON, #50H
	MOV	TMOD, #20H
	MOV	TH1, #252
	SETB	TR1

MAIN:
	ACALL	SELECT_ROW
	ACALL	GET_LENGTH
	ACALL	TRANSMIT_ROW
	SJMP	MAIN

SELECT_ROW:
	JNB	P1.0, SECOND_ROW
	MOV	DPTR, #ROW1
	RET
SECOND_ROW:
	MOV	DPTR, #ROW2
	RET

GET_LENGTH:
	CLR	A
	MOVC	A, @A+DPTR
	INC	DPTR
	MOV	R7, A		;LENGTH
	RET

TRANSMIT_ROW:
	ACALL	READ_BYTE
	ACALL	TRANSMIT_BYTE
	ACALL	DISPLAY_INFO
	DJNZ	R7, TRANSMIT_ROW
	RET

READ_BYTE:
	CLR	A
	MOVC	A, @A+DPTR
	INC	DPTR
	RET

TRANSMIT_BYTE:
	MOV	SBUF, A
	JNB	TI, $
	CLR	TI
	RET

DISPLAY_INFO:
	MOV	P0, A		;CHAR BEING TRANSMITTED
	DEC	R7
	MOV	P2, R7		;CHARS LEFT TO BE TRANSMITTED
	INC	R7
	RET

ROW1:	DB	12, 'THIS IS ROW1'
ROW2:	DB	12, 'THIS IS ROW2'
	END
